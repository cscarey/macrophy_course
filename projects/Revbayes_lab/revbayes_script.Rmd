## Rev Analysis Script

x_obs <- readDataDelimitedFile(file="./data/x.csv", header=TRUE, delimiter=",")[1]
y_obs <- readDataDelimitedFile(file="./data/y.csv", header=TRUE, delimiter=",")[1]

m ~ dnNormal(0, 1)
b ~ dnNormal(0, 1)
sigma ~ dnExponential(1)

## We have defined 3 stochastic nodes in our model. Basically, we've defined priors
## for our parameters. Have a look at what it means to be a stochastic node in revbayes:
m
m.redraw()
m.probability()
m.lnProbability()


## Now, for each observed value in x_obs we will create a deterministic 
## node for mu_y and a stochastic node for y:
for (i in 1:x_obs.size()) {
  mu_y[i] := (m * x_obs[i]) + b
  y[i] ~ dnNormal(mu_y[i], sigma)
}

for(i in 1:y.size()) {
  y[i].clamp(y_obs[i])
}

mymodel = model(m)

moves[1] = mvSlide(m, delta=0.001, weight=1)
moves[2] = mvSlide(b, delta=0.001, weight=1)
moves[3] = mvSlide(sigma, delta=0.001, weight=1)

monitors[1] = mnScreen()
monitors[2] = mnModel("./output/linear_regression1.log")

mymcmc = mcmc(mymodel, moves, monitors)
mymcmc.run(10000)

## Try again while changing the weights of the slide move.
moves[1] = mvSlide(m, delta=1, weight=1)
moves[2] = mvSlide(b, delta=1, weight=1)
moves[3] = mvSlide(sigma, delta=1, weight=1)

monitors[1] = mnScreen()
monitors[2] = mnModel("./output/linear_regression2.log")

mymcmc = mcmc(mymodel, moves, monitors)
mymcmc.run(10000)

################################################
########### Phylogenetic Models ################
################################################

## adapted from exercise by:
## Sebastian Hoehna, Michael Landis, and Tracy A. Heath

### Read in sequence data for the gene
data = readDiscreteCharacterData("data/primates_and_galeopterus_cytb.nex")

# Get some useful variables from the data. We need these later on.
num_taxa <- data.ntaxa()
num_branches <- 2 * num_taxa - 3
taxa <- data.taxa()


mvi = 1
mni = 1

# specify the stationary frequency parameters
pi_prior <- v(1,1,1,1) 
pi ~ dnDirichlet(pi_prior)
moves[mvi++] = mvBetaSimplex(pi, weight=2.0)
moves[mvi++] = mvDirichletSimplex(pi, weight=1.0)


# specify the exchangeability rate parameters
er_prior <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_prior)
moves[mvi++] = mvBetaSimplex(er, weight=3.0)
moves[mvi++] = mvDirichletSimplex(er, weight=1.5)


# create a deterministic variable for the rate matrix, GTR
Q := fnGTR(er,pi) 

##############
# Tree model #
##############

out_group = clade("Galeopterus_variegatus")
# Prior distribution on the tree topology
topology ~ dnUniformTopology(taxa, outgroup=out_group)
moves[mvi++] = mvNNI(topology, weight=num_taxa/2.0)
moves[mvi++] = mvSPR(topology, weight=num_taxa/10.0)

# Branch length prior
for (i in 1:num_branches) {
    bl[i] ~ dnExponential(10.0)
    moves[mvi++] = mvScale(bl[i])
}

TL := sum(bl)

psi := treeAssembly(topology, bl)




###################
# PhyloCTMC Model #
###################

# the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=Q, type="DNA")

# attach the data
seq.clamp(data)


############
# Analysis #
############

mymodel = model(psi)

# add monitors
monitors[mni++] = mnScreen(TL, printgen=1000)
monitors[mni++] = mnFile(psi, filename="output/primates_cytb_GTR.trees", printgen=10)
monitors[mni++] = mnModel(filename="output/primates_cytb_GTR.log", printgen=10)

# run the analysis
mymcmc = mcmc(mymodel, monitors, moves, nruns=2)
mymcmc.run(generations=20000,tuningInterval=200)


# summarize output
treetrace = readTreeTrace("output/primates_cytb_GTR.trees", treetype="non-clock", outgroup=out_group)
# and then get the MAP tree
map_tree = mapTree(treetrace,"output/primates_cytb_GTR_MAP.tre")

